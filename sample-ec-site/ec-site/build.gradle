plugins {
    alias(libs.plugins.spring.boot.plugin) apply false
    alias(libs.plugins.spring.dependency.management) apply false
    alias(libs.plugins.jig.plugin) apply false
}

allprojects {

}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.dddjava.jig-gradle-plugin'

    group = 'com.myou.ec'
    version = '0.0.1-SNAPSHOT'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    repositories {
        mavenCentral()
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    dependencies {
        annotationProcessor libs.lombok
        implementation platform(libs.spring.boot.bom)
        implementation(platform(libs.spring.cloud.dependencies))
        implementation(platform(libs.spring.cloud.aws.dependencies))
    }

    tasks.named('test') {
        useJUnitPlatform()
    }
}

project(":boot") {
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    dependencies {
        implementation project(':presentation')
        implementation project(':application')
        implementation project(':infrastructure')
        implementation project(':domain')

        implementation libs.spring.boot.starter.actuator
        implementation libs.spring.boot.starter.data.jdbc
        implementation libs.spring.boot.starter.data.redis
        implementation libs.spring.boot.starter.security
        implementation libs.spring.boot.starter.thymeleaf
        implementation libs.spring.boot.starter.web
        implementation libs.spring.cloud.starter.config
        implementation libs.spring.cloud.starter.bus.amqp
        implementation libs.spring.session.data.redis
        implementation libs.thymeleaf.extras.springsecurity6
        implementation libs.awspring.cloud.starter
        implementation libs.awspring.cloud.starter.s3
        implementation libs.awspring.cloud.starter.sqs
        implementation libs.awspring.cloud.starter.parameter.store
        implementation libs.awspring.cloud.starter.secrets.manager
        implementation libs.awspring.cloud.starter.metrics
        implementation libs.awspring.cloud.aws.testcontainers

        implementation libs.micrometer.tracing.bridge.otel
        implementation libs.opentelemetry.exporter.otlp

        implementation libs.micrometer.registry.prometheus
        implementation libs.logback.access.spring
        implementation libs.logback.access.tomcat

        developmentOnly libs.spring.boot.devtools
        runtimeOnly libs.h2
        testImplementation libs.spring.boot.starter.test
        testImplementation libs.spring.security.test
        testImplementation libs.apache.httpcomponents.client5

    }
}

project(":presentation") {

    dependencies {
        implementation project(':application')
        implementation project(':domain')

        implementation libs.spring.boot.starter.actuator
        implementation libs.spring.boot.starter.web
        implementation libs.spring.boot.starter.thymeleaf
        implementation libs.spring.session.data.redis
        implementation libs.spring.cloud.starter.config

        implementation libs.micrometer.tracing.bridge.otel

        testImplementation libs.spring.boot.starter.test
        testImplementation libs.spring.security.test
        testImplementation libs.apache.httpcomponents.client5

    }
}

project(":application") {
    apply plugin: 'java-library'

    dependencies {
        implementation project(':domain')

        implementation libs.spring.context

        testImplementation libs.spring.boot.starter.test

    }
}

project(":infrastructure") {
    apply plugin: 'java-library'

    dependencies {
        implementation project(':application')
        implementation project(':domain')

        implementation libs.spring.boot.starter.data.jdbc
        implementation libs.spring.boot.starter.data.redis
        implementation libs.awspring.cloud.starter
        implementation libs.awspring.cloud.starter.s3
        implementation libs.apache.httpcomponents.client5

        runtimeOnly libs.h2

        testImplementation libs.spring.boot.starter.test
    }
}

project(":domain") {
    apply plugin: 'java-library'

    dependencies {

    }
}